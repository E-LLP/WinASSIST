(* ASSIST model for *)
(* Fighter Combat using Dave Lee's construct for blue missile counting*)
(* This version includes missile failures *)
(* The kill rate is for one-on-one and is multiplied for N killers *)
(* Assume one missile per kill even with N killers *)
(* Therefore, the kill rate includes one hit and all misses *)
(* Find miss rate based on ratio of Pmiss:Pk *) 
(* last edit 8/1/01 *)


 (* TEST FOR DUPLICATE STATES *)
LIST = 3;
ONEDEATH OFF;   (* enumerate all death states *)

(* equipment list *)
nblue = 3;            (* blue fighters *)
nred = 3;             (* red fighters *)

 (* kill rates *)
k_rate_blue = 0.01;               (* blue kill rate *)
k_rate_red = 0.005;                (* red kill rate *)

pk = 0.85;                           (* missile kill probability *)
miss_rate = (1-pk)/pk*k_rate_blue;   (*  missile miss rate *)

SPACE = (n0:0..nblue,n1:0..nblue,n2:0..nblue,n3:0..nblue,n4:0..nblue,n5:0..nblue,n6:0..nblue,red:0..nred);
START = (0,0,0,0,0,0,nblue,nred);

DEATHIF (n0=0) and (n1=0) and (n2=0) and (n3=0) and (n4=0) and (n5=0) and (n6=0);
DEATHIF (red = 0);

(* transitions: Blue kill *)
IF (n6>0) AND (red>0) TRANTO n6=n6-1, n5=n5+1, red=red-1 BY n6*k_rate_blue;
IF (n5>0) AND (red>0) TRANTO n5=n5-1, n4=n4+1, red=red-1 BY n5*k_rate_blue; 
IF (n4>0) AND (red>0) TRANTO n4=n4-1, n3=n3+1, red=red-1 BY n4*k_rate_blue;
IF (n3>0) AND (red>0) TRANTO n3=n3-1, n2=n2+1, red=red-1 BY n3*k_rate_blue;
IF (n2>0) AND (red>0) TRANTO n2=n2-1, n1=n1+1, red=red-1 BY n2*k_rate_blue;
IF (n1>0) AND (red>0) TRANTO n1=n1-1, n0=n0+1, red=red-1 BY n1*k_rate_blue;

(* transitions: Blue misses *)
IF (n6>0) AND (red>0) TRANTO n6=n6-1, n5=n5+1 BY n6*miss_rate;
IF (n5>0) AND (red>0) TRANTO n5=n5-1, n4=n4+1 BY n5*miss_rate;
IF (n4>0) AND (red>0) TRANTO n4=n4-1, n3=n3+1 BY n4*miss_rate;
IF (n3>0) AND (red>0) TRANTO n3=n3-1, n2=n2+1 BY n3*miss_rate;
IF (n2>0) AND (red>0) TRANTO n2=n2-1, n1=n1+1 BY n2*miss_rate;
IF (n1>0) AND (red>0) TRANTO n1=n1-1, n0=n0+1 BY n1*miss_rate;

(* transitions: Red kills *)
IF (n6>0) AND (red>0) TRANTO n6=n6-1 BY (n6/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (n5>0) AND (red>0) TRANTO n5=n5-1 BY (n5/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (n4>0) AND (red>0) TRANTO n4=n4-1 BY (n4/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (n3>0) AND (red>0) TRANTO n3=n3-1 BY (n3/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (n2>0) AND (red>0) TRANTO n2=n2-1 BY (n2/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (n1>0) AND (red>0) TRANTO n1=n1-1 BY (n1/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
IF (red>0) AND (n0>0) TRANTO n0=n0-1 BY (n0/(n0+n1+n2+n3+n4+n5+n6))*red*k_rate_red;
